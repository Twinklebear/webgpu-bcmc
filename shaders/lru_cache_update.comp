#version 450 core

#include "lru_cache_inputs.comp"

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

layout(set = 1, binding = 0, std430) buffer NewItemIDs
{
    uint new_items[];
};

layout(set = 2, binding = 0, std140) uniform WorkItemOffset
{
    uint work_item_offset;
    uint total_work_groups;
    uint num_new_items;
};

void main(void)
{
    const uint work_item_id = work_item_offset * 32 + gl_GlobalInvocationID.x;
    if (work_item_id >= num_new_items) {
        return;
    }
    const uint item = new_items[work_item_id];
    const uint slot = slot_available_id[work_item_id];
    const int prev = slot_data[slot].item_id;
    // Evict the previous item, if there was one in this slot
    if (prev != -1) {
        cached_item_slot[prev] = -1;
    }

    slot_data[slot].age = 0;
    slot_data[slot].item_id = int(item);
    slot_data[slot].available = 0;
    cached_item_slot[item] = int(slot);
}

