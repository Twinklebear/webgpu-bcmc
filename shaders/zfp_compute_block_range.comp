#version 450 core

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

#include "zfp_decompress.comp"
#include "util.glsl"

// Note: This could really be done by the file format or server ahead of time,
// instead of having the client do a preprocess to compute these ranges
layout(set = 0, binding = 2, std430) buffer BlockInformation
{
    vec2 block_ranges[];
};

layout(set = 1, binding = 0, std140) uniform BlockIDOffset
{
    uint block_id_offset;
};

const uvec3 index_to_vertex[8] = {
    uvec3(0, 0, 0), // v000 = 0
    uvec3(1, 0, 0), // v100 = 1
    uvec3(0, 1, 0), // v010 = 2
    uvec3(1, 1, 0), // v110 = 3
    uvec3(0, 0, 1), // v001 = 4
    uvec3(1, 0, 1), // v101 = 5
    uvec3(0, 1, 1), // v011 = 6
    uvec3(1, 1, 1)  // v111 = 7
};

void main(void)
{
    const uint block_index = gl_GlobalInvocationID.x + block_id_offset * 32;
    const uint total_blocks = ((padded_dims.x * padded_dims.y) / 64) * padded_dims.z;

    if (block_index >= total_blocks) {
        return;
    }

    BlockReader reader = create_block_reader(block_index);
    float decompressed_block[ZFP_BLOCK_SIZE];
    decompress_block(reader, decompressed_block);

    // No support for strided volumes
    const uvec3 stride = uvec3(1, volume_dims.x, volume_dims.x * volume_dims.y);

    uvec3 nblocks;
    nblocks.x = padded_dims.x >> 2;
    nblocks.y = padded_dims.y >> 2;
    nblocks.z = padded_dims.z >> 2;

    uvec3 block;
    block.x = (block_index % nblocks.x) * 4;
    block.y = ((block_index / nblocks.x) % nblocks.y) * 4;
    block.z = (block_index / (nblocks.x * nblocks.y)) * 4;

    vec2 block_range = vec2(1e20f, -1e20f);
    bvec3 partial = greaterThan(block + 4, volume_dims.xyz);
    uvec3 partial_size = uvec3(partial.x ? volume_dims.x - block.x : 4,
            partial.y ? volume_dims.y - block.y : 4,
            partial.z ? volume_dims.z - block.z : 4);

    for (uint z = 0; z < partial_size.z; ++z) {
        for (uint y = 0; y < partial_size.y; ++y) {
            for (uint x = 0; x < partial_size.x; ++x) {
                block_range.x = min(block_range.x, decompressed_block[16 * z + 4 * y + x]);
                block_range.y = max(block_range.y, decompressed_block[16 * z + 4 * y + x]);
            }
        }
    }
    block_ranges[block_index] = block_range;
}
